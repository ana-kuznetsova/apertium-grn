OPTIONS += no-inline-sets ;

SUBREADINGS = LTR ;

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

SETS

LIST N = n ;

LIST Incp = incp ;

LIST Guess = guess ;

LIST Prn = prn ;

LIST Personal = pers ;

LIST Ind = ind ;

LIST A = "a" ;

LIST Re = "re" ;

LIST Num = num ;

LIST Sg = sg ;

LIST Pl = pl ;

LIST Np = np ;

LIST Subs = subs ;

LIST Top = top ;

LIST Cog = cog ;

LIST Ant = ant ;

LIST Det = det ;

LIST Cnjcoo = cnjcoo ;

LIST V = v ;

LIST Tv = tv ;

LIST Iv = iv ;

LIST Aux = aux ;

LIST Coop = coop ;

LIST Adj = adj ;

LIST Dem = dem ;

LIST P3 = p3 ;

LIST P1 = p1 ;

LIST Post = post ;

LIST Adv = adv;

LIST Ac = ac ;

LIST In = in ;

LIST Acc = ac ;

LIST Barb = barb ;

LIST Pres = pres ;

LIST Ser = vbser ;

LIST Ref = ref ;

LIST Lang = "ñeʼẽ" ;
LIST Peoples = "guarani" "guaraní" ;
LIST Petei = "peteĩ" ;

SET SubSg = Ant | Top | Cog ; # Subject Sg Without N
SET Verbal = V | Aux ;
SET Pers = P1 | P3 ;

SECTION

#HEURISTICS

REMOVE SUB:3 Incp ;

REMOVE SUB:2 Incp ;

REMOVE SUB:1 Incp ;

REMOVE Incp ;

REMOVE A + Prn (0/* N) ;

REMOVE Re + Prn (0/* N) ;

REMOVE Guess ;

REMOVE Pl IF (1 Petei) ;
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

SELECT Prn + Ind IF (1 N) ;
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

#SELECT ("ñemoñare") IF (-1* (N) OR (Np Ant)) ;

REMOVE Top IF ((1 Cog) OR (1 Ant)) ;
# León Cadogan omoĩ o kuatiañe'ẽme

SELECT Post IF ((-1 Top) OR (-2 Top)) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe

#GRAMMATICAL

SELECT N IF (-1 Petei);
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

SELECT Sg + V IF ((-1 SubSg) OR (-2 SubSg));
# León Cadogan omoĩ o kuatiañe'ẽme

SELECT Cnjcoo IF ((-1 N LINK 2 N) OR (-1 Np LINK 2 Np));
#Avañe'ẽ ha Guarani ñe'ẽ

SELECT Det + Pl IF (-1 N) ;
# óga kuéra

SELECT Adj + Dem + Pl IF (1/* Det + Pl) ;
# umi Guaranikuéra

SELECT Pl IF (1 Aux + Pl) ;
#Avañe'ẽ ha Guarani ñe'ẽ ombohéra hikuái

SELECT Adv IF (1/* V) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

SELECT Sg + In IF (-1*/* Petei); #Careful!
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

REMOVE Sg IF ((-2/* Pl) OR (-1/* Pl)) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

#SELECT Ref IF (-1/* V + Sg) ;
#oñemohendáva in
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava,
#oñemohendáva irundy tetãnguéra iñambuévape (Paraguái, Argentina, Volívia ha Vrasil)

SELECT Ser IF ((-1 N LINK 2 Num)) ;

######################################
#NOUN WITH AN ATTRIBUTE CONSTRUCTIONS#
######################################

SELECT Adj + Dem IF (1/* N);
#ko órape

SELECT Num IF (1/* N) ;
#mokõi óga

SELECT Adj + Ind IF (1/* N) ;
#ambue tetã

SELECT Adj IF (-1/* N);
SELECT N IF (1/* Adj);
#kyse pyahu

REMOVE Adj IF (0/1 Adj + Dem LINK -2/* Adj + Dem) ; # Fix for peva
#pe kuña péva

SELECT Prn + Dem IF (1/1 Adj + Dem LINK 1/1 N) ; #FIX
SELECT N IF (-1/* Adj + Dem) ;
#kóva ko távape

SELECT Prn + Personal IF (1/* N LINK 1/* Adj);
#nde mitã michĩ

#WORD FORMS

REMOVE Barb IF (0/* Top); 

SELECT N IF (0 Lang) (-1C Peoples) ;
# Avañe'ẽ ha Guarani ñe'ẽ ombohéra hikuái (...)

AFTER-SECTIONS

SELECT SUB:* Pres IF (0/* Pres) ;