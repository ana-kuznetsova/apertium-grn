Alphabet

A B Ã C D E Ẽ G G̃ H I Ĩ J K L M N Ñ O Õ P R S T U Ũ V W X Y Z Ỹ Á É Í Ó Ú Ý 
a b ã c d e ẽ g g̃ h i ĩ j k l m n ñ o õ p r s t u ũ v w x y z ỹ á é í ó ú ý ʼ


%{J%}:j %{J%}:ñ
%{P%}:ʼ %{P%}:0
%{I%}:0 %{I%}:j %{I%}:ñ %{I%}:ʼ
%{H%}:0 %{H%}:h
%{K%}:k %{K%}:g 
%{N%}:0 %{N%}:n 
%{D%}:d %{D%}:0 
%{T%}:0
%{p%}:0 %{p%}:r
%{E%}:i %{E%}:e
%{d%}:d %{d%}:0 
%{r%}:0 %{r%}:r
•:0
%{o%}:0
%{e%}:e %{e%}:a %{e%}:o 
%{Y%}:0 %{Y%}:y 
%{m%}:m %{m%}:p
%{t%}:0

%>:0 
% 
%'
%+
;

Sets

Nas = m n ñ ã ẽ ĩ õ ỹ ũ
      M N Ñ Ã Ẽ Ĩ Õ Ỹ Ũ ;

Cns = B C D F G H J K L M N P Q R S T V W X Z G̃ Ñ 
      b c d f g h j k l m n p q r s t v w x z g̃ ñ ;

ArchiCns = %{N%} %{K%} ;

CnsNorm = B C D F G H J K L P Q R S T V W X Z
          b c d f g h j k l p q r s t v w x z ;

Vows = A Ã E Ẽ I Ĩ O Õ U Ũ Y Ỹ Á É Í Ó Ú Ý
       a ã e ẽ i ĩ o õ u ũ y ỹ á é í ó ú ý ;

VowsTon = Á É Í Ó Ú Ý
          á é í ó ú ý ;

VowsAton = A E I O U Y 
           a e i o u y ;

VowsStrong = A Ã E Ẽ O Õ Á É Ó
             a ã e ẽ o õ á é ó ;

VowsWeak = I Ĩ U Ũ Y Ỹ Í Ú Ý
           i ĩ u ũ y ỹ í ú ý ;

Rules

"Remove morpheme boundary"
%>:0 <=> _ ; 

"Remove stress marker"
•:0 <=> _ ;

"Change vowel before atonal suffixes"
Va:Vt <=> _ %>: [m: a: | n: t: e: | i: c: h: a: | %{m%}: e: | g: u: i: | r: e:] ;
            [Cns: | VowsAton: | Nas: ]+ _ %>: %{r%}: ;
            _ %{o%}: [v: | a:] ;
            except
            [VowsTon: | Cns: | VowsAton:]+ _ %>: [m: a: | n: t: e: | i: c: h: a:|v: | a:] ;
            where Va in ( a e o u y ) 
                  Vt in ( á é ó ú ý ) matched ;

"Change i to tonal before atonal suffixes"
i:í <=>  _ %>: [m: a: | n: t: e:| p: e:] ;
        _ %{o%}: [v: | a:] ; 

! NOTE: The i--í is taken care of by another rule, to avoid conflict with i:0

"Delete ending -[i] before comparative -icha" 
Vx:0 <=> _ %>: [ i: c: h: a: | %{r%}: i: ] ;
            where Vx in ( ĩ i í ) ;

!@ n:n {d}:d >:0 a:a {P}:0 m:m o:o m:m i:i c:c h:h ĩ:0 >:0 {r}:0 i:i

"Change tonal vowel to atonal if tonal in affix"
Vt:Va <=> •: _ [ Cns: | %>: | ArchiCns: | Nas: | VowsAton: ]+ VowsTon: ;
          •: _ [ Cns: | %>: | ArchiCns: | Nas: | VowsAton: ]+ %{E%}: ;
           where Vt in VowsTon Va in VowsAton matched ; 

!@ •:0 ó:o g:g a:a >:0 {N}:0 {K}:k u:u é:é r:r a:a 

!@ •:0 ó:ó g:g a:a >:0 p:p e:e

!@ •:0 ó:o g:g a:a >:0 g:g u:u a:a

"Add nasal prefix [ñ] in present 1pl incl"
%{J%}:ñ <=> _ a: CNAS: ;
                    where CNAS in Nas ;

"Insert glottal stop ['] if next morpheme starts with a vowel"
%{P%}:ʼ <=> _ [ VowsTon: | •: VowsAton:] ;

"Surface [n] in plural nouns" 
%{N%}:n <=> Nas: %>: _ ;

"Surface [g] in pl nouns after nasals" 
%{K%}:g <=> Nas: %>: %{N%}: _ ;

"Possessive {H} before VowsTon"
%{H%}:h <=> _ i: %{I%}: VowsTon: ;


"Remove underlying [i] in 3p possessive prefixes"
%{I%}:0 <=>  _ CnsNorm: ;

"Surface ʼ in possessive prefix i"
%{I%}:ʼ <=> _ VowsTon: ;

"Possessive {I} as j before VowsAton" 
%{I%}:j <=> _ VowsAton: ;
            except 
            _ VowsAton:[CnsNorm: | VowsAton: |VowsTon: ]+ Nas: ; 


"Possessive %{I%} as ñ in nasal nouns starting with VowsAton"
%{I%}:ñ <=> _ VowsAton:[CnsNorm: | VowsAton: |VowsTon: ]+ Nas: ;

"Remove [d] in possessive prefixes" 
%{D%}:0 <=> _ e: [CnsNorm: | VowsAton: |VowsTon: ]+ Nas: ;

"Surface [e] after weak vowels in indefinite adjectives" 
%{E%}:e <=> VowsWeak: %>: _ ;

"Remove [d] in negative verbs before nasals"
%{d%}:0 <=> _ %>: [CnsNorm: | VowsAton: |VowsTon: ]+ Nas: ;

"%{r%} as r in negative verbs ending with i"
%{r%}:r <=> i: %>: _ ;

"Surface [y] in postpositions"
%{Y%}:y <=> g: u: a: %>: _ g: u: a: ;

"Surface [m] in postpositions after nasals"
%{m%}:m <=> [VowsTon: |VowsAton:| CnsNorm:]+ [Nas:]+ [VowsTon: |VowsAton:| CnsNorm:]+ %>: _ ;

"Remove surface [t] in Triformes"
t:0 <=> %{t%}: _ ;